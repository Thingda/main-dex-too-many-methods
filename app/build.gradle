apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "info.osom.multidex"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
	
	buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:multidex:1.0.1'
}

/*  Filters main-dex classes
 *  ========================
 *  Excludes activity classes from main-dex
 *  For more details see: http://blog.osom.info/2014/12/too-many-methods-in-main-dex.html
 */
afterEvaluate {
    project.tasks.each { task ->
        if (task.name.startsWith('collect') && task.name.endsWith('MultiDexComponents')) {
            println "main-dex-filter: found task $task.name"
            task.filter { name, attrs ->
                def componentName = attrs.get('android:name')
                if ('activity'.equals(name)) {
                    println "main-dex-filter: skipping, detected activity [$componentName]"
                    return false
                } else {
                    println "main-dex-filter: keeping, detected $name [$componentName]"
                    return true
                }
            }
        }
    }
}